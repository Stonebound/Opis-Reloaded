buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = project.version
group = project.group
archivesBaseName = project.name

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpMappings
    makeObfSourceJar = false
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    shade 'org.apache.commons:commons-math3:3.2'
    shade 'org.jfree:jcommon:1.0.21'
    shade 'org.jfree:jfreechart:1.0.17'
    shade 'com.miglayout:miglayout-swing:4.2'
    shade 'net.sf.opencsv:opencsv:2.3'
    shade('org.spongepowered:mixin:0.7.2-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }
    compile 'info.journeymap:journeymap-api:1.12-1.4'
}

def root = project.projectDir.parentFile

sourceSets {
    main {
        refMap = "mixins.opis.refmap.json"
        java {
            srcDir new File(root, "src")
        }
        resources {
            srcDir new File(root, "resources")
        }
    }
}

task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

artifacts {
    archives sourceJar, devJar
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) exclude 'META-INF', 'META-INF/**', 'LICENSE.txt'
    }
    manifest {
        attributes(
                'MixinConfigs': 'mixins.opis.json',
                'ForceLoadAsMod' : true,
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        )

    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

}
